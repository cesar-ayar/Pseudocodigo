# Ejemplos de pseudocódigo

*Por **Cesar Ayar***

Ahora luego de una atiborración de conocimiento, veamos todo esto de manera práctica, es el chiste, ¿no?

## Adivina que número pensé

Este es un juego simple en donde debes adivinar un número aleatorio con una cantidad de intentos.

He aquí el código a utilizar, puedes copiarlo y pegarlo o bien escribirlo en PSeInt:

```
// Juego simple que pide al usuario que adivine un numero en 10 intentos

Proceso Adivina_Numero
    
    intentos<-9; //Aquí defines la cantidad de intentos del jugador.
    
    num_secreto <- azar(100)+1; // Esta línea genera el número a adivinar.
    
    Escribir "Adivine el número (de 1 a 100):"; //Imprime el texto en pantalla.
    
    Leer num_ingresado; //Pide que el usuario escriba el número que cree.

    Mientras num_secreto<>num_ingresado Y intentos>0 Hacer //Establece que mientras que el número secreto y el número ingresado sean diferentes Y que la cantidad de intentos sea mayor a 0, ejecute estos procesos.
        
        Si num_secreto>num_ingresado Entonces //En este SI, estas indicando que si num_secreto es MAYOR QUE num_ingresado ejecute la acción
        
            Escribir "Muy bajo"; //Aquí se establece que si la condición es VERDADERO, escriba Muy bajo.

        Sino  //Establece que hacer en caso de que la condición no se cumpla.

            Escribir "Muy alto"; //Si la condición resulta FALSO, imprimirá Muy alto

        FinSi //Establece el final de la condición.

        Escribir "Le quedan ",intentos," intentos:"; //Imprime la cantidad de intentos que tienes aún.

        Leer num_ingresado; //Vuelve a pedir que ingresen un número.

        intentos <- intentos-1; //Resta uno a la cantidad de intentos al cumplir estas reglas.
    
    FinMientras //Establece el final de las condicionales.

Si intentos=0 Entonces //Inicia una nueva condición SI.
    
    Escribir "El numero era: ",num_secreto; //Devuelve la variable num_secreto si la iteración devuelve un TRUE

Sino //Establece que hacer en los demás casos.

    Escribir "Exacto! Usted adivinó en ",11-intentos," intentos."; //Devuelve la cantidad de intentos que uso en caso de que la condición devuelva FALSE

FinSi //Termina el ciclo condicional

FinProceso 
```

¡Lo ves! Es simple, quizás ahora todo te confunda, pero conforme comprendas los conceptos y la lógica que usa un programador, se te irá facilitando poco a poco.

¿Usaste el juego? ¿Entretenido? Nahhhhh, son demasiados intentos, hagamoslo un poco más díficil el juego:

1. Haz que en lugar de 10 intentos, solo sean 3 intentos.
2. 100 son demasiados números, haz que sean números del 1 al 10.

Por último, guarda el código que acabas de modificar en una carpeta llamada "Ejercicios_Pseudocódigo", dale a tu archivo el nombre de "Juego_AdivinaNumero".

## Mayores

En este programa te encargarás de buscar en una lista N de datos cuales son los dos datos mayores, puedes poner una lista de las edades de tu familia, de tus compañeros, o de lo que quieras.

```
// Busca los dos mayores de una lista de N datos
Proceso Mayores
    
    Dimension datos[10]; //Establece la dimensión de datos
    
    Escribir "Ingrese la cantidad de datos:"; //Imprime en pantalla la indicación al usuario
    
    Leer n; //Recibe el conjunto de datos
    
    Para i<-1 Hasta n Hacer //Establece que se repita esta indicación hasta que alcances la dimensión de datos
        
        Escribir "Ingrese el dato ",i,":"; // Imprime instrucción en pantalla
        
        Leer datos[i]; // Recibe el conjunto de datos
    
    FinPara //Cierra tu ciclo de recepción de datos.
    
    Si datos[1]>datos[2] Entonces //Comienza a comparar los datos
    
        may1<-datos[1];
        
        may2<-datos[2];
    
    Sino //Ejecuta esta acción si la condición anterior no se cumple
        
        may1<-datos[2]; // Establece el primer valor
    
        may2<-datos[1]; //Establece el segundo valor
    
    FinSi //Termina el bloque condicional.
    
    Para i<-3 Hasta n Hacer //Indica que hasta que recorra todo el array de datos debe ejecutar este bloque.
        
        Si datos[i]>may1 Entonces //Si algun numero es mayor que may1
        
            may2<-may1; //Entonces redefines su valor a may2
            
            may1<-datos[i]; // El número del array almacenalo en may1
        
        Sino //Ejecuta esta acción si no se cumple la condición anterior.
        
            Si datos[i]>may2 Entonces //Compara los datos y almacenalos en caso de que sea verdadero.
            
                may2<-datos[i]; // Establece la variable
            
            FinSi //Cierra el ciclo que abriste dentro del SI
        
        FinSi // Cierra el ciclo SI
    
    FinPara //Cierra tu iteración de comparación

Escribir "El mayor es: ",may1; //Devuelve el primer número mayor

Escribir "El segundo mayor es: ",may2; //Devuelve el segundo número mayor

FinProceso

```

¿Confundido? Esta bien la idea que comiences a ver casos de uso reales.

En este programa ingrese un concepto nuevo que es **i**, esto se utiliza como *buena práctica* cuando trabajas con arrays, es la abreviación de index, que básicamente es la forma de saber con que dato estas trabajando, ya que en una serie de 10 o 20 números es simple, pero ¿te imaginas cuando tienes una base de datos como Facebook? Son millones de datos, por ello se establece como i, para que cualquiera que modifique el código comprenda que esa variable es el index, sin embargo puedes definirla con cualquier letra.

Ahora es tiempo de que lo modifiques, tu misión, si decides aceptarla es:

1. Recibe un total de 6 datos en lugar de 10.
2. Haz que devuelva los número menores en lugar de los más grandes del conjunto de datos.

Guarda el código que acabas de modificar en tu carpeta llamada "Ejercicios_Pseudocódigo", dale a tu archivo el nombre de "Devuelve_datos".

## El enredo de Pitágoras

¿Te gusta la trigonometría? A mí no, la odio, pero sigues viendola en muchas cosas, así que por ello es mejor tener un programa que te simplifique estos cálculos tediosos, ¿no?

```
// cargar datos
Escribir "Ingrese el lado 1:";
Leer l1;
Escribir "Ingrese el lado 2:";
Leer l2;
Escribir "Ingrese el lado 3:";
Leer l3;
// encontrar la hipotenusa (mayor lado)
Si l1>l2 Entonces
        cat1<-l2;
        Si l1>l3 Entonces
            hip<-l1;
            cat2<-l3;
        Sino
            hip<-l3;
            cat2<-l1;
        FinSi
    Sino
        cat1<-l1;
        Si l2>l3 Entonces
            hip<-l2;
            cat2<-l3;
        Sino
            hip<-l3;
            cat2<-l2;
        FinSi
    FinSi
// ver si cumple con Pitágoras
Si hip^2 = cat1^2 + cat2^2 Entonces
    // calcular área
    area<-(cat1*cat2)/2;
    Escribir "El área es: ",area;
Sino
    Escribir "No es un triángulo rectángulo.";
FinSi
```

¿Simple no? Con esto me ahorre muchos procesos nerdos.

Tu tarea esta vez sera totalmente diferente, ahora en lugar de modificar el código, quiero que con comentarios, expliques la lógica detrás de el.

*Para ingresar un comentario simplemente pon doble diagonal, así:

```
// Así se pone un comentario.
```

Guarda el código que acabas de modificar en tu carpeta llamada "Ejercicios_Pseudocódigo", dale a tu archivo el nombre de "Enredo_Pitágoras".

## Ordena mi desorden

El orden es una de las peores cualidades del hombre, no perdemos la cabeza porque la tenemos pegada, olvide almacenar en orden una lista, cómo todo, pues diseñe el proceso para que quede en orden.

```
Proceso OrdenaLista 

Dimension lista[20];

Escribir "Ingrese los nombres (enter en blanco para terminar):";

// leer la lista

cant<-0;

Leer nombre;

Mientras nombre<>"" Hacer
    
    cant<-cant+1;
    
    lista[cant]<-nombre;

    Repetir // leer un nombre y ver que no esté ya en la lista
        Leer nombre;
        
        se_repite<-Falso;
        
        Para i<-1 Hasta cant Hacer
            
            Si nombre=lista[i] Entonces
            
                se_repite<-Verdadero;
            
            FinSi
        
        FinPara
    
    Hasta Que NO se_repite

FinMientras

// ordenar
Para i<-1 Hasta cant-1 Hacer
        
        // busca el menor entre i y cant        
        pos_menor<-i;

    Para j<-i+1 Hasta cant Hacer
            
            Si lista[j]<lista[pos_menor] Entonces
            
            pos_menor<-j;
        
        FinSi
    
    FinPara
    
// intercambia el que estaba en i con el menor que encontro
    aux<-lista[i];
    
    lista[i]<-lista[pos_menor];
    
    lista[pos_menor]<-aux;

FinPara

// mostrar cómo queda la lista
Escribir "La lista ordenada es:";

Para i<-1 Hasta cant Hacer

Escribir " ",lista[i];

FinPara

FinProceso
```

Bueno, este es el código, como en todos juega un poco con el y hazle algunos cambios.

1. Mi lista creció y ahora tengo 30 datos, haz el cambio para que reciba esos 30 datos
2. Haz que después de imprimir la lista, imprima el texto: "Gracias por el desorden que dejaste".

Guarda el código que acabas de modificar en tu carpeta llamada "Ejercicios_Pseudocódigo", dale a tu archivo el nombre de "Ordenando_Desorden".

## ¿Pase mi materia?

Cuando estaba en la secundaria, mi primer código fue uno para calcular mi promedio, te muestro la estructura que le dí

```
Proceso Promedio
    Escribir "Ingrese la cantidad de datos:";
    Leer n;
    
    acum<-0;
    
    Para i<-1 Hasta n Hacer
        Escribir "Ingrese el dato ",i,":";
        Leer dato;
        acum<-acum+dato;
    FinPara

    prom<-acum/n;
    
    Escribir "El promedio es: ",prom;
FinProceso
```

¿Muy simple no? Así ya no tenía que hacer cuentas y eso. Era muy flojo en ese entonces.

Ahora bien, hay algunos cambios que se me ocurren y que te tocará poner en práctica todo lo que ya viste, tu tarea será modificarlo y hacer lo siguiente:

1. Haz que comparte el promedio en escala de 0 a 10, donde si es mayor a 7 sea APROBADO, en caso contrario que indique que esta REPROBADO.
2. Modifica el código a modo de que te devuelva las dos materias que mejor calificacíon obtienes.

Guarda el código que acabas de modificar en tu carpeta llamada "Ejercicios_Pseudocódigo", dale a tu archivo el nombre de "¿Pase_Mi_Materia?".

### ¿Es todo?

Bueno, estos son todos los ejemplos que te muestran el pseudocódigo, es uno de los lenguajes más simples, pues es muy parecido a nuestro lenguaje, esto te ayuda a aprender la lógica de las computadoras, no te preocupes si no te sale a la primera, nadie lo entiende de volada, puedes volver a leer, buscar en Google, preguntarme o lo que te acomode, recuerda que muchas veces la solución es más simple de lo que piensas, el inicio de la lógica de programación puede ser muy tedioso, pero tu puedes, solo tomalo con calma, razona e inclusive en tu cuaderno escribe como tu razonas el problema.

Y ante todo recuerda el principio de oro en el software, el principio KISS, no no es besense con la máquina o con tus colegas.

##### K

Keep

##### I

It

#### S

Simple,

#### S

Stupid.

Así que, mantenlo simple, no todos son unos genios en la computadora, hay que hacer que los mortales lo entiendan.
